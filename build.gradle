plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.openapi.generator" version "7.10.0"
}

group = 'dev.jadnb'
version = '0.0.1-SNAPSHOT'
def rootUri = project.rootDir.toURI().toString()

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

sourceSets {
    main {
        java {
            srcDir("$buildDir/generated/src/main/java")
        }
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

openApiValidate {
    inputSpec.set("${rootUri}spec/shoppingList.yaml")
}

openApiGenerate {
    generatorName.set("spring")
    inputSpec.set("${rootUri}spec/shoppingList.yaml")
    outputDir.set("${rootDir}/build/generated")
    apiPackage.set("dev.jadnb.shoppinglist.api")
    invokerPackage.set("dev.jadnb.shoppinglist.invoker")
    modelPackage.set("dev.jadnb.shoppinglist.model")
    configOptions.put("delegatePattern", "true")
    configOptions.put("useSpringBoot3", "true")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation group: 'org.flywaydb', name: 'flyway-database-postgresql', version: '11.1.0'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.4'
    implementation group: 'jakarta.validation', name: 'jakarta.validation-api', version: '3.1.0'
    implementation group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.2.6'
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.27'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}
